10 " "USING :CLS :CURSOR 9:PAUSE "H";CHR$ 95;"NGM";CHR$ 95;"N":PAUSE "  Copr. 1982 Tandy Corp."
20 CLEAR :DIM A$(1)*26,S(1),N(1):RANDOM :ON ERROR GOTO 950:RESTORE :FOR I=1TO RND 45:READ A$(1):NEXT I
30 WAIT 0:INPUT "1 or 2 players? ";N
40 IF N<>1AND N<>2THEN 30
50 G=(N=2)
100 IF N=2LET G=1-G
110 N(G)=N(G)+1:IF N=1THEN 300
200 INPUT "Word? ";A$(1):GOTO 220
210 GOTO 200
220 E=0:FOR I=1TO LEN A$(1):J=ASC MID$ (A$(1),I,1):IF J<65OR J>90LET E=1
230 NEXT I:IF ETHEN 200
240 GOTO 400
300 FOR I=1TO 1+5*RND 5
310 READ A$(1):NEXT I
400 A$(0)="ABCDEFGHIJKLMNOPQRSTUVWXYZ":M=0:R=0
410 WAIT 0:CLS :FOR I=1TO LEN A$(1):PRINT CHR$ 95;:NEXT I
500 L$=INKEY$ :IF L$="*"END
510 IF L$<"A"OR L$>"Z"THEN 500
520 L=ASC L$-64:IF MID$ (A$(0),L,1)<>L$BEEP 3:GOTO 500
600 A$(0)=LEFT$ (A$(0),L-1)+"#"+RIGHT$ (A$(0),LEN A$(0)-L)
610 J=0:FOR I=1TO LEN A$(1):CURSOR I-1:PRINT CHR$ 127;:BEEP 1,20,40:CURSOR I-1:T$=MID$ (A$(1),I,1)
620 IF T$=L$PRINT L$:BEEP 1,9,99:J=1:R=R+1:GOTO 650
630 IF MID$ (A$(0),ASC T$-64,1)="#"PRINT T$;:GOTO 650
640 PRINT CHR$ 95;
650 NEXT I
700 IF J=0LET M=M+1:BEEP 1,100,150:CURSOR 17:WAIT 0:PRINT M;" missed";:GOTO 720
710 IF R=LEN A$(1)THEN 800
720 IF M>6THEN 900
730 GOTO 500
800 S(G)=S(G)+1:BEEP 8,9,30:CLS :WAIT 3:PRINT "R";:FOR I=1TO 21:PRINT "I";:NEXT I:PRINT "GHT!"
810 FOR I=0TO 19:CURSOR I:PRINT " R";:NEXT I:CURSOR 0:PAUSE " ";
850 CLS :IF N=2PRINT "Player";G+1;": ";
860 WAIT 120:PRINT S(G);" out of";N(G):GOTO 100
900 FOR I=1TO 3:BEEP 1,99,200:BEEP 1,150,120:NEXT I:WAIT 99:CLS :PRINT "You missed this one!"
910 PRINT "It was ";CHR$ 34;A$(1);CHR$ 34:GOTO 850
950 RESTORE :GOTO 310
1000 DATA "COMPUTER","PROGRAM","COMMITTEE","PAPERBACK","TANGERINE"
1010 DATA "AIRLINER","ANNOUNCEMENT","NEWSPAPER","UNIVERSITY","ELEVATOR"
1020 DATA "POLITICS","STAIRWELL","BALLPOINT","TAILFIN","BILLBOARD"
1030 DATA "NOTEPAD","STAPLER","BIRDBATH","VAMPIRE","SNAPSHOT"
1040 DATA "ENVELOPE","THUNDERSTORM","SUNSHINE","RESERVOIR","EQUATION"
1050 DATA "BOOKKEEPER","PROJECTION","HOROSCOPE","WRISTWATCH","AUTOMATIC"
1060 DATA "VERMILLION","TRASHCAN","BRIEFCASE","MEMORANDUM","BATTERY"
1070 DATA "STATUE","GATEWAY","BOULEVARD","CONVERTIBLE","SURVIVE"
1080 DATA "CIRCUIT","STARSHIP","BRIARS","FLAGPOLE","VIBRATE"
2000 END
