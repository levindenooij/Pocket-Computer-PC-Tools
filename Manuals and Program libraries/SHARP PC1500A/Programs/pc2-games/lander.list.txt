10 " "LOCK :CLEAR :USING :CLS :CURSOR 10:PAUSE "LANDER":PAUSE "  Copr. 1982 Tandy Corp."
100 WAIT 0:CLEAR :DEGREE :VH=7572:A=132000:FU=99:G=5.36233:DR=100:PR=10560000
110 INPUT "Speed X real time? ";S:IF S>=.1AND S<=10THEN 200
120 WAIT 60:PRINT "Reply 1-10":WAIT 0:GOTO 110
200 VV=VV-S*(VH^2/(PR+A))+G*S
300 RF=VAL INKEY$ :DE=S*RF*32.174:VV=VV-DE*SIN T:VH=VH-DE*COS T:FU=FU-.2*RF*S
400 T=ATN (VV/VH):IF VH<0LET T=T+180
402 IF T<0LET T=T+360
405 A=A-VV*S:V=SQR (VV^2+VH^2):DR=DR-VH*S/5280:IF A<0THEN 600
410 IF FU<=0WAIT :PRINT "OUT OF FUEL!!!":GOTO 700
500 PRINT USING "#####";V;USING "#######";A;USING "####.#";T;USING "###";FU;USING "#####";DR:GOTO 200
600 WAIT 90:IF V>100PRINT "  ***** ker-SPLAT! *****":T=0:GOTO 620
610 IF V>20PRINT "THUD!"
620 USING :PRINT "You have arrived on Luna":IF T>=70AND T<=110PRINT "(rolling and bouncing)"
630 PRINT INT DR;" miles off target":WAIT :PRINT "at";INT V;" feet per second."
700 Z$="":INPUT "Play again? (Y/N) ";Z$
710 IF Z$="Y"THEN 100
720 IF Z$<>"N"THEN 700
730 UNLOCK :END
